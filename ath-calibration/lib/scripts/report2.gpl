
#load 'param.txt'
load PARAM_FILE

set terminal pngcairo enhanced monochrome size R_W,R_H font "Arial,11"
set output R_FILE.'.png'

set multiplot layout 2,2 \
              margins 0.1,0.96,0.1,0.92 \
              spacing 0.1,0.06 \
			  title R_TITLE font "Arial,13" noenhanced

set label 'https://at-horns.eu' center at screen 0.5,0.02 tc rgb 0xa0a0a0

#--------------------------------------------------------
# Polar Map

set contour base
set view map
unset surface
set style textbox noborder opaque
set cntrparam level discrete 1,-1,-3,-6,-10,-20
set cntrlabel start -3 interval -1 font "Courier New,11"
set grid xtics mxtics ytics
set xtics add ("200" 0.2, "500" 0.5, "1k" 1, "5k" 5, "10k" 10, "" 15, "20k" 20)
set logscale x
set format y "%.0f°"
set xrange [0.2:20]
set yrange [0:R_MAX_ANGLE]
set ylabel "Polar map [dB SPL]" offset -2,0
splot "pmap.txt" u 2:1:3 w l lw 2 not, \
      "" u 2:1:3 w labels boxed not
	  
#--------------------------------------------------------
# Polar curves + Sound Power

#set palette model RGB
#set palette rgbformulae 33,13,10
#set palette rgb 3,11,6

set grid xtics mxtics ytics
set logscale x
unset colorbox
#set yrange [-36:6]
set yrange [-20:70]
set ytics ("3" 3,"0" 0,"-6" -6,"-12" -12,"-18" -18,"-24" -24,"-30" -30)
set ylabel "dB SPL (".R_NORM_ANGLE."° normalized)" offset 0,0
unset format y
plot "polars.txt" u 1:2:3 not w l lw 2 palette, \
     "spower_norm.txt" u 1:2 w l dt "-" lc rgb "black" lw 2 t "SP"

#--------------------------------------------------------
# Throat Impedance
	
set yrange [0:2]
set ytics ("0" 0, "0.2" 0.2, "0.5" 0.5, "1" 1, "1.5" 1.5)
set xlabel "Frequency [Hz]"
set ylabel "Throat Impedance"
plot "radimp.txt" u 1:2 w l lw 2 lc rgb "black" t "Re", "" u 1:3 w l lw 1 t "Im"

#--------------------------------------------------------
# DI, etc.

set yrange [0:30]
set ytics ("0" 0, "6" 6, "12" 12, "18" 18, "24" 24)
set xlabel "Frequency [Hz]"
set ylabel "Directivity Index [dB]" offset -1,0
plot "DI.txt" u 1:2 w l lw 2 t "on-axis", \
	 "" u 1:3 w l lw 2 t "10 deg", \
	 "" u 1:4 w l lw 2 t "20 deg"

unset multiplot

