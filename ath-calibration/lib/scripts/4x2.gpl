load 'static.txt'
load 'param.txt'

set terminal pngcairo monochrome size R_W,R_H font "Arial,11"
set output R_FILE.'.png'

set multiplot layout 4,2 \
              margins 0.1,0.96,0.08,0.93 \
              spacing 0.1,0.02 \
			  title R_TITLE font "Arial,14" noenhanced

set label R_DIM at screen 0.1,0.95 font "Arial,11"
set label 'Driver: '.R_DRIVER at screen 0.3,0.95 font "Arial,11"
			  
# profile sketch
set size ratio -1
set border 2
unset xtics
set ytics 50 out nomirror
set yrange [0:R_MAX_R]
set ylabel "[mm]"
plot 'coords.txt' u 1:2 w l lw 1 not, 0 lt 4 not

set size noratio 1,1
set border 31
set ytics in auto

set xrange [200:20000]
set xtics add ("" 200, "" 500, "" 1000, "" 5000, "" 10000, "" 15000, "" 20000)
set logscale x

# raw SPL curves + Sound Power
set grid xtics mxtics ytics
unset colorbox
set yrange [R_MIN_SPL:R_MAX_SPL]
set ylabel "dB SPL / ".R_VRMS." / ".R_DIST offset 0,0
unset format y
plot "polars.txt" u 1:2:3 not w l lw 1 palette, \
     "spower.txt" u 1:2 w l dt "-" lc rgb "black" lw 2 t "SP"

# Polar Map
set contour base
set view map
unset surface
set style textbox noborder opaque
set cntrparam level discrete 1,-1,-3,-6,-10,-20
set cntrlabel start -3 interval -1 font "Courier New,10"
set grid xtics mxtics ytics
set format y "%.0f°"
set yrange [0:R_MAX_ANGLE]
set ylabel "Polar map [dB SPL]" offset -2,0
splot "pmap.txt" u 2:1:3 w l lw 1 not, "" u 2:1:3 w labels boxed not
  
# normalized SPL curves + Sound Power
set grid xtics mxtics ytics
unset colorbox
set yrange [-35:5]
set ytics ("0" 0,"-5" -5,"-10" -10,"-15" -15,"-20" -20,"-25" -25,"-30" -30)
set ylabel "dB SPL (".R_NORM_ANGLE."° normalized)" offset 0,0
unset format y
plot "polars_norm.txt" u 1:2:3 not w l lw 1 palette, \
     "spower_norm.txt" u 1:2 w l dt "-" lc rgb "black" lw 2 not

# Throat Impedance
set yrange [0:2]
set ytics ("0" 0, "0.2" 0.2, "0.5" 0.5, "1" 1, "1.5" 1.5)
set ylabel "Throat Impedance" offset -1,0
plot "radimp.txt" u 1:2 w l lw 2 lc rgb "black" t "Re", "" u 1:3 w l lw 1 t "Im"

# DI, etc.
set yrange [0:30]
set ytics ("0" 0, "5" 5, "10" 10, "15" 15, "20" 20, "25" 25)
set ylabel "Directivity Index [dB]" offset -1,0
plot "DI.txt" u 1:2 w l lw 2 t "on-axis", \
	 "" u 1:3 w l lw 2 t "10 deg", \
	 "" u 1:4 w l lw 2 t "20 deg"
	
set xlabel "Frequency [Hz]"
set xtics add ("200" 200, "500" 500, "1k" 1000, "5k" 5000, "10k" 10000, "" 15000, "20k" 20000)
	
# Driver impedance
set	yrange [0:R_MAX_DRVIMP]
set ytics 10 nomirror
set ylabel "Driver Impedance [Ohm]" offset -1,0
set y2range [-90:90]
set y2tics 30
set format y2 "%.0f°"
plot "drvimp.txt" u 1:2 w l lw 2 t "Mag", \
     "" u 1:3 axes x1y2 w l lw 1 t "Phase"
 
# Excursion
set yrange [0:0.5]
set ytics 0.1
unset y2tics
set ylabel "Peak Excursion [mm]" offset 1,0
plot "excursion.txt" u 1:2 w l lw 2 t R_VRMS, \
     "" u 1:3 w l lw 1 t "110 dB / ".R_DIST
	 
unset multiplot
